buildscript {
    repositories {
        mavenLocal()
        google()
        jcenter()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://plugins.gradle.org/m2/" }
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.2'
        classpath "de.mannodermaus.gradle.plugins:android-junit5:1.4.0.0"
        classpath 'org.jacoco:org.jacoco.core:0.8.0'
    }
}
description = "Very cool game, totally not towerfall"
allprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"
    apply plugin: "jacoco"

    version = '1.0'
    ext {
        appName = "cool-game"
        gdxVersion = '1.9.9'
        roboVMVersion = '2.3.5'
        box2DLightsVersion = '1.4'
        ashleyVersion = '1.7.0'
        aiVersion = '1.8.0'
        junitVersion = '5.4.0'
        kryonetVersion = '2.22.0-RC1-local'
    }

    repositories {
        mavenLocal()
        maven { url "${rootDir}/localMaven" }
        jcenter()
        google()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    }


    jacoco {
        toolVersion = '0.8.0'
    }
    tasks.withType(Test) {
        jacoco.includeNoLocationClasses = true
    }
}
project(":desktop") {
    description = "Desktop client"
    apply plugin: "java"


    dependencies {
        compile project(":core")
        compile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        compile "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-desktop"
        compile "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
        compile "com.badlogicgames.gdx:gdx-tools:$gdxVersion"
    }
}
project(":android") {
    description = "Android client"
    apply plugin: "android"
    apply plugin: "de.mannodermaus.android-junit5"

    configurations { natives }

    dependencies {
        implementation project(":core")
        implementation "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi-v7a"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-arm64-v8a"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86_64"
        implementation "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"
        natives "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-armeabi"
        natives "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-armeabi-v7a"
        natives "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-arm64-v8a"
        natives "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-x86"
        natives "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-x86_64"
        implementation "com.badlogicgames.box2dlights:box2dlights:$box2DLightsVersion"
        implementation "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
        natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-armeabi"
        natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-armeabi-v7a"
        natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-arm64-v8a"
        natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-x86"
        natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-x86_64"
        implementation "com.badlogicgames.ashley:ashley:$ashleyVersion"
    }
}
project(":core") {
    description = "Common code for clients"
    apply plugin: "java-library"

    // Exclude all views from test coverage, because they are untestable
    // because of Stage, SpriteBatch and shaders.
    jacocoTestCoverageVerification {
        afterEvaluate {
            classDirectories = files(classDirectories.files.collect {
                fileTree(dir: it,
                        exclude: [
                                'no/ntnu/tdt4240/g17/cool_game/screens/**/*View*.class'
                        ]
                )
            })
        }
    }

    dependencies {
        implementation project(":common")
        api "com.badlogicgames.gdx:gdx:$gdxVersion"
        implementation "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"
        implementation "com.badlogicgames.box2dlights:box2dlights:$box2DLightsVersion"
        implementation "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
        implementation "com.badlogicgames.ashley:ashley:$ashleyVersion"
        implementation "com.esotericsoftware:kryonet:$kryonetVersion"

        testImplementation "org.junit.jupiter:junit-jupiter:$junitVersion"

        testImplementation "com.badlogicgames.gdx:gdx-backend-headless:$gdxVersion"
        testImplementation "com.badlogicgames.gdx:gdx:$gdxVersion"
        testImplementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        testImplementation "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"
        testImplementation "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-desktop"
    }
}
project(":server") {
    description = "Main game server"
    apply plugin: 'java'

    dependencies {
        implementation project(":common")

        // Maven doesn't have code sources, and no idea which commit it was made from.
        // This one is made from commit 2ed19b4743667664d15e3778447e579855ba3a65
        // From https://github.com/EsotericSoftware/kryonet.git
        implementation "com.esotericsoftware:kryonet:$kryonetVersion"

        implementation "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-desktop"
        implementation "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"
        implementation "com.badlogicgames.ashley:ashley:$ashleyVersion"

        testImplementation "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
        testImplementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    }

    task printDeps {
        doLast {
            println "Dependencies:"
            configurations.each{
                try {
                    println(it.name + ">>>>>>>")
                    it.files.each{ println it }
                } catch (err) {}
            }
            //configurations[':server:compile'].files.each { println it }
        }
    }
}
project(":common") {
    description = "Code that is common for both client and server"

    apply plugin: 'java-library'

    dependencies {
    }

}
subprojects {
    sourceSets {
        main.java.srcDirs = ['src/main/java']
        main.resources.srcDirs = ['src/main/resources']
        test.java.srcDirs = ['src/test/java']
        test.resources.srcDirs = ['src/test/resources']
    }

    dependencies {
        implementation 'org.jetbrains:annotations:17.0.0'

        implementation "org.slf4j:slf4j-api:1.7.26"
        testImplementation "org.slf4j:slf4j-api:1.7.26"

        compileOnly 'org.projectlombok:lombok:1.18.6'
        testCompileOnly 'org.projectlombok:lombok:1.18.6'
        annotationProcessor 'org.projectlombok:lombok:1.18.6'

        testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
        testImplementation "org.mockito:mockito-core:2.21.0"
        testImplementation "org.hamcrest:hamcrest-all:1.3"
        implementation 'junit:junit:4.12'
    }
}
configure(subprojects.findAll { it.name != "android" }) {
    // Some things just don't work in an android module.
    // Enable such things here.

    apply plugin: "checkstyle"
    checkstyle {
        toolVersion = '8.18'
        checkstyleTest.enabled = false
    }

    dependencies {
        implementation "ch.qos.logback:logback-classic:1.2.3"
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "passed", "skipped", "failed"
        }
    }
    test.finalizedBy jacocoTestReport
    check.dependsOn jacocoTestCoverageVerification
    jacocoTestCoverageVerification {
        violationRules {
            rule {
                limit {
                    counter = 'LINE'
                    value = 'COVEREDRATIO'
                    minimum = 0.6
                }
            }
        }
    }

    jacocoTestReport {
        reports {
            xml.enabled true
            csv.enabled false
            html.destination file("${buildDir}/jacocoHtml")
        }
    }
}
tasks.eclipse.doLast {
    delete ".project"
}
dependencies {
}